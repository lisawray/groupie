// Top-level build file where you can add configuration options common to all sub-projects/modules.

def lintReportsDir = file("${rootProject.buildDir}/lint-reports")
def testReportsDir = "${rootProject.buildDir}/test-reports"
def testResultsDir = "${rootProject.buildDir}/test-results"

buildscript {
    ext.kotlin_version = '1.9.10'
    ext.android_plugin_version = '7.2.2'
    ext.sdkVersion = 33
    ext.minimumSdkVersion = 14
    ext.databinding_version = '7.2.2'
    ext.viewbinding_version = '7.2.2'

    ext.junit_version = '4.13.2'
    ext.mockito_version = '3.3.3'

    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$android_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.18"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects { project ->
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
    }

    afterEvaluate {
        if (project.hasProperty('android')) {
            android {
                // use a custom location for the Lint reports so that it's easy to archive them in Circle CI
                lintOptions {
                    htmlOutput file("$lintReportsDir/${project.name}/lint-report.html")
                }
                // it would be nice to do the same with test results/reports
                // but it doesn't work: https://issuetracker.google.com/issues/37132023
                testOptions {
                    reportDir "$testReportsDir/${project.name}"
                    resultsDir "$testResultsDir/${project.name}"
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
